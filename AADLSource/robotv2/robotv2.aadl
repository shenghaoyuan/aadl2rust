package Robot_BA
public
  with Data_Model, Base_Types;

  ----------
  -- Data --
  ----------

  data Alpha_Type
  properties
    Data_Model::Data_Representation => Boolean;
  end Alpha_Type;

  -------------------
  --  Subprograms  --
  -------------------

  subprogram action_spg
  features
    d_action : in parameter Alpha_Type;
  properties
    source_language => (C);
    source_name     => "action";
    source_text     => ("robot.c");
  end action_spg;

  ---------------
  --  Threads  --
  ---------------

  -- interfaces
  thread capteur
  features
    evenement    : out event data port Alpha_Type;  
  end capteur;



  thread servomoteur
  features
    ordre        : in event data port Alpha_Type;
  end servomoteur;

  thread controle
  features
    info_capteur : in event data port Alpha_Type;
    comm_servo   : out event data port Alpha_Type;
  end controle;

  thread implementation controle.i
  properties
    Dispatch_Protocol => Periodic;
    Period            => 110 ms;
  annex Behavior_specification {**
    variables
      comm_false : Base_Types::Boolean := false;
      comm_true  : Base_Types::Boolean := true;
    states
        s_inline : initial complete final state;
        s1, s2 : state;
        s_outline : complete state;
    transitions
        s_inline -[on dispatch]-> s1;
        s1 -[info_capteur]-> s_inline;
        s1 -[not info_capteur]-> s_outline {
          comm_servo := comm_false
        };

        s_outline -[on dispatch]-> s2;
        s2 -[not info_capteur]-> s_outline;
        s2 -[info_capteur]-> s_inline {
          comm_servo := comm_true
        };
  **};
  end controle.i;
  thread implementation capteur.i
  properties
    Dispatch_Protocol => Periodic;
    Period            => 110 ms;
    Compute_Execution_Time => 0 ms .. 1 ms;
  annex Behavior_specification {**
    variables
      count1 : Base_Types::Integer;
    states
      s0 : initial complete final state;
    transitions
      s0 -[on dispatch]-> s0 {
        computation(1 ms);
        count1 := count1 + 1;
        evenement := (count1 mod 2 = 0)
      };
  **};
  end capteur.i;

  -- thread implementation controle.i
  -- properties
  --   Dispatch_Protocol => Periodic;
  --   Period            => 110 ms;
  --   Compute_Execution_Time => 0 ms .. 1 ms;
  -- annex Behavior_specification {**
  --   states
  --       s_inline : initial complete final state;
  --       s1, s2 : state;
  --       s_outline : complete state;

  --   transitions
  --       s_inline -[on dispatch]-> s1;
  --       s1 -[info_capteur]-> s_inline;
  --       s1 -[not info_capteur]-> s_outline {
  --         comm_servo := false
  --       };

  --       s_outline -[on dispatch]-> s2;
  --       s2 -[not info_capteur]-> s_outline;
  --       s2 -[info_capteur]-> s_inline {
  --         comm_servo := true
  --       };
  -- **};
  -- end controle.i;

  thread implementation servomoteur.i
  calls main : {
    A_Spg : subprogram action_spg;
    };
  connections
    conn1: parameter ordre -> A_Spg.d_action;
  properties
    Dispatch_Protocol => Sporadic;
    Period    => 10 ms;
    Compute_Execution_Time => 0 ms .. 1 ms;
  end servomoteur.i;

  -----------------
  --  Processes  --
  -----------------

  process p_capteur
  features
    evenement : out event data port Alpha_Type;
  end p_capteur;

  process p_controle
  features
    info_capteur_droit  : in event data port Alpha_Type;
    comm_servo_droit    : out event data port Alpha_Type;
    info_capteur_gauche : in event data port Alpha_Type;
    comm_servo_gauche   : out event data port Alpha_Type;
  end p_controle;

  process p_servomoteur
  features
    ordre : in event data port Alpha_Type;
  end p_servomoteur;

  process implementation p_capteur.i
  subcomponents
    th_c : thread capteur.i;
  connections
    conn1: port th_c.evenement -> evenement;
  end p_capteur.i;

  process implementation p_controle.i
  subcomponents
    th_ctrl_droit  : thread controle.i;
    th_ctrl_gauche : thread controle.i;
  connections
    conn1: port info_capteur_droit        -> th_ctrl_droit.info_capteur;
    conn2: port th_ctrl_droit.comm_servo  -> comm_servo_droit;
    conn3: port info_capteur_gauche       -> th_ctrl_gauche.info_capteur;
    conn4: port th_ctrl_gauche.comm_servo -> comm_servo_gauche;
  end p_controle.i;

  process implementation p_servomoteur.i
  subcomponents
    th_servomoteur : thread servomoteur.i;
  connections
    conn1: port ordre -> th_servomoteur.ordre;
  end p_servomoteur.i;

  --------------
  --  System  --
  --------------

  processor the_processor
  end the_processor;

  processor implementation the_processor.impl
  properties
    Scheduling_Protocol => (RMS);
  end the_processor.impl;

  system robot
  end robot;

  system implementation robot.i
  subcomponents
    proc_capteur_droit      : process p_capteur.i;
    proc_capteur_gauche     : process p_capteur.i;

    proc_controle           : process p_controle.i;

    proc_servomoteur_droit  : process p_servomoteur.i;
    proc_servomoteur_gauche : process p_servomoteur.i;

    CPU1                    : processor the_processor;
  connections
    conn1: port proc_capteur_droit.evenement    -> proc_controle.info_capteur_droit;
    conn2: port proc_capteur_gauche.evenement   -> proc_controle.info_capteur_gauche;

    conn3: port proc_controle.comm_servo_droit  -> proc_servomoteur_droit.ordre;
    conn4: port proc_controle.comm_servo_gauche -> proc_servomoteur_gauche.ordre;
  properties
    actual_processor_binding => reference (CPU1) applies to proc_capteur_droit;
    actual_processor_binding => reference (CPU1) applies to proc_capteur_gauche;
    actual_processor_binding => reference (CPU1) applies to proc_controle;
    actual_processor_binding => reference (CPU1) applies to proc_servomoteur_droit;
    actual_processor_binding => reference (CPU1) applies to proc_servomoteur_gauche;
  end robot.i;

end Robot_BA;
