--  This AADL model illustrates how to model a simple interaction
--  between tasks, in a local or distributed environment.
--
--  One task pings another, sending a data periodically.
--
--  $Id: software.aadl 344 2007-04-17 14:48:16Z zalila $

package Software
public
  with Data_Model;

----------
-- Data --
----------

data Simple_Type
properties
  Data_Model::Data_Representation => integer;
end Simple_Type;

-----------------
-- Subprograms --
-----------------

subprogram produce_packet_init_spg
properties
  source_language => (C);
  source_name     => "user_produce_pkts_init";
  source_text     => ("pinger.c");
end produce_packet_init_spg;

subprogram produce_packet_spg
properties
  source_language => (C);
  source_name     => "user_produce_pkts";
  source_text     => ("pinger.c");
end produce_packet_spg;

subprogram Do_Ping_Spg
features
  data_source : out parameter Simple_Type;
properties
  source_language => (C);
  source_name     => "user_do_ping_spg";
  source_text     => ("pinger.c");
end Do_Ping_Spg;

subprogram Ping_Spg
features
  data_sink : in parameter Simple_Type;
properties
  source_language => (C);
  source_name     => "user_ping_spg";
  source_text     => ("pinger.c");
end Ping_Spg;


-------------
-- Threads --
-------------

thread packet_producer
end packet_producer;

thread implementation packet_producer.i
calls
Mycalls: {
  produce : subprogram produce_packet_spg;
};
properties
  Initialize_Entrypoint              => classifier (software::produce_packet_init_spg);
  Dispatch_Protocol                  => Periodic;
  Period                             => 500 Ms;
  Deadline                           => 500 ms;
  Priority                           => 2;
end packet_producer.i;

thread P
features
  data_source : out event data port Simple_Type;
end P;

thread implementation P.Impl
calls
Mycalls: {
  P_Spg : subprogram Do_Ping_Spg;
};
connections
  cn0: parameter P_Spg.data_source -> data_source;
properties
  Recover_Entrypoint_Source_Text     => "recover";
  Dispatch_Protocol                  => Periodic;
  Period                             => 1000 Ms;
  Deadline                           => 1000 ms;
  Priority                           => 2;
end P.Impl;

thread Q
features
  data_sink : in event data port Simple_Type;
end Q;

thread implementation Q.Impl
calls
Mycalls: {
  Q_Spg : subprogram Ping_Spg;
};
connections
  cn0: parameter data_sink -> Q_Spg.data_sink;
properties
  Dispatch_Protocol                  => Sporadic;
  -- Period                             => 10 Ms;
  -- deadline                           => 10 Ms;
  -- Priority                           => 1;
end Q.Impl;

end Software;
