// aadl.pest

//匹配时，并非“贪心算法”，浅尝辄止
// “|”该符号不能作为开头的首个符号

WHITESPACE = _{ " " | "\t" | "\r" | "\n" | COMMENT }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" | "--" ~ (!"\n" ~ ANY)* }

// 基础标识符规则
identifier = @{ 
    ASCII_ALPHA ~ 
    //(ASCII_ALPHANUMERIC | '_' .. '.')*  // 不知道为什么不行
    (ASCII_ALPHANUMERIC | '_' .. '_' | '.'..'.')*  
}
no_point_identifier = @{ 
    ASCII_ALPHA ~ 
    (ASCII_ALPHANUMERIC | '_' .. '_' )*  
}

qualified_identifier = @{ (identifier ~ "::")* ~ identifier }
package_name = { qualified_identifier }
property_set_name = { identifier }

// 4.2 Package 这里的public和private信息被丢弃
package_declaration = {
    "package" ~ package_name ~ 
    ("public" | "private")? ~
    visibility_declarations* ~
    package_sections ~
    "end" ~ package_name ~ ";"
}

visibility_declarations = {
    (WITH ~ (package_name | property_set_name) ~ ("," ~ (package_name | property_set_name))* ~ ";" )|
    (identifier ~ "renames" ~ ("package" ~ package_name | qualified_identifier) ~ ("as" ~ identifier)? ~ ";")
}
WITH = { "with" }

package_sections = {
    ((PUBLIC | PRIVATE)?  ~ declaration*)? 
}
PUBLIC = { "public" }
PRIVATE = { "private" }


declaration = {
    component_type |
    component_implementation |
    annex_library
}

// 4.3 Component Types ，似乎processor与process存在冲突，暂时给processor取名cjprocessor
// 上述问题已解决，把processor放在process之前
component_category = {
    "abstract" |
    "data" | "subprogram" | "subprogram group" |
    "thread" | "thread group" | "processor" |
    "memory" | "process" | "bus" | "device" | "virtual processor" | "virtual bus" |
    "system"
}

component_type = {
    component_category ~ identifier ~
    prototypes? ~
    features? ~
    properties? ~
    annexes? ~
    "end" ~ identifier ~ ";"
}

// 4.4 Component Implementations
component_implementation = {
    component_category ~ "implementation" ~ identifier ~ 
    prototypes? ~
    subcomponents? ~
    calls? ~
    connections? ~
    properties? ~
    annexes? ~
    "end" ~ identifier ~ ";"
}

// 4.5 Subcomponents
subcomponent = {
    identifier ~ ":" ~ component_category ~ identifier ~
    array_spec? ~
    properties? ~
    ";"
}

// 4.7 Prototypes TODO
prototype_declaration = {
    identifier ~ ":" ~
    ("component" ~ component_category ~ ("classifier" ~ qualified_identifier)?) |
    ("feature" ~ ("in" | "out" | "in out")? ~ ("classifier" ~ qualified_identifier)?) |
    ("feature group" ~ ("classifier" ~ qualified_identifier)?)
}

// 8 Features
feature_declaration = {
    identifier ~ ":" ~ direction? ~ port_type ~ identifier? ~ ";"
}

direction = { "in out" | "in" | "out" } // 注意 "in out" 要放前面

port_type = { "data port" | "event data port" | "event port" | "parameter" }

// 9 Connections
connection = {
    identifier ~ ":" ~ connection_type ~
    (port_connection | parameter_connection) ~
    ";"
}

connection_type = { "port" | "parameter" }
connection_operator = { "->" | "<->" }

port_connection = {
    port_reference ~ connection_operator ~ port_reference
}

parameter_connection = {
    parameter_reference ~ connection_operator ~ parameter_reference
}

// 11 Properties 这里其实合并了property_association和contained_property_association（有applies子句，移动到了property_value的reference_value里）
property_association = {
    identifier ~
    ( "::" ~ property_set_name )? ~
    property_operator ~
    constant? ~ property_value ~
    ";"
}

property_operator = { "=>" | "+=>" }

constant = { "constant" }


property_value = {
    range_value | //优先于literal_value匹配
    literal_value | //字面量
    list_value |
    reference_value 
}

literal_value = { (number | string_literal | boolean | enum_value ) ~ unit? } 
range_value = { 
    number ~ WHITESPACE* ~ unit? ~ WHITESPACE* ~ 
    ".." ~ WHITESPACE* ~ 
    number ~ WHITESPACE* ~ unit? 
}
list_value = { "(" ~ property_value ~ ("," ~ property_value)* ~ ")" }
reference_value = { "reference" ~ "(" ~ qualified_identifier ~ ")" ~ ( "applies" ~ "to" ~ qualified_identifier )? }

sign = { "+" | "-" }
dot = { "." }
number = { sign? ~ numbervalue} //带符号整数或浮点数
numbervalue = {ASCII_DIGIT+ ~ (dot ~ ASCII_DIGIT+)?}

unit = { no_point_identifier } //可选的单位
string_literal = { 
    "\"" ~ (!"\"" ~ ANY)* ~ "\"" | 
    "(" ~ ("()" | ( !")" ~ ANY ) )* ~ ")" 
}//双引号字符串或括号内容（如 "text" 或 (content)）
//注意跳过("new PingPongType()") 内部的括号对

boolean = { "true" | "false"}
// 枚举值规则（覆盖AADL属性集预定义值）
enum_value = @{
    "Periodic" | "Sporadic" | "Timed" | "Event" | "Hybrid" |
    "native"
}

// 5.2 Subprogram Calls
call_sequence = {
    identifier ~ ":" ~ "{" ~ subprogram_call+ ~ "}" ~ ";"
}

subprogram_call = {
    identifier ~ ":" ~ "subprogram" ~ qualified_identifier ~ ";"
}

// Annexes
annex_library = { "annex" ~ identifier? ~ "{" ~ ANY* ~ "}" ~ ";" }
annex_subclause = { "annex" ~ identifier? ~ "{" ~ ANY* ~ "}" ~ ";" }

// Helpers
array_spec = { "[" ~ dimension ~ ("," ~ dimension)* ~ "]" }
dimension = { number? }
//mode_reference_list = { identifier ~ ("," ~ identifier)* }
qualified_identifier_list = { qualified_identifier ~ ("," ~ qualified_identifier)* }
port_reference = { (no_point_identifier ~ ".")? ~ no_point_identifier }
parameter_reference = { (identifier ~ ".")? ~ identifier }

// 子句可选性
prototypes = { "prototypes" ~ (prototype_declaration+ | "") }
features = { "features" ~ (feature_declaration+ | "") }
properties = { "properties" ~ (property_association+ | "") }
annexes = { "annexes" ~ (annex_subclause+ | "") }
subcomponents = { "subcomponents" ~ (subcomponent+ | "") }
calls = { "calls" ~ (call_sequence+ | "") }
connections = { "connections" ~ (connection+ | "") }


file = { SOI ~ package_declaration ~ EOI }