-- 系统功能：演示包含数据端口和事件端口的混合通信模式
--   - 线程Producer：周期性线程（周期1000ms），产生Mission类型数据并发送
--   - 线程Filter：周期性线程（周期1000ms），过滤处理生产者的数据
--   - 线程Consumer：偶发线程，接收并消费过滤后的数据

-- 系统特点：
-- 1. 使用复杂数据结构Mission，包含纬度、经度、高度三个整型字段，以数组形式表示
-- 2. 采用混合通信模式：数据端口（data port）和事件端口（event port）组合使用
-- 3. 生产者到过滤器：同时使用数据端口（to_filter_data）和事件端口（to_filter_event）
-- 4. 过滤器到消费者：使用事件数据端口（event data port）
-- 5. 明确的栈大小配置（60KB）和消费者队列大小配置（队列大小为1）
-- 6. 支持多平台部署选项（JVM、Linux、macOS、seL4等）

package PFC
public
	with Base_Types, Data_Model;

	data Coordinate
		properties
			Data_Model::Data_Representation => Struct;
	end Coordinate;

	data implementation Coordinate.Impl
		subcomponents
			latitude: data Base_Types::Integer_32;
			longitude: data Base_Types::Integer_32;
			altitude: data Base_Types::Integer_32;
	end Coordinate.Impl;

	data Mission
		properties
			Data_Model::Data_Representation => Array;
			Data_Model::Base_Type => (classifier (Coordinate.Impl));
			Data_Model::Dimension => (3);

			-- HAMR::Bit_Codec_Spec => "data/Mission.sc";
			-- HAMR::Bit_Codec_Max_Size => 288 Bits;
	end Mission;

	system PFC_Sys
	end PFC_Sys;

	system implementation PFC_Sys.Impl
		subcomponents
			proc: process PFC_Proc.Impl;
		-- properties
			-- HAMR::Platform => (JVM, Cygwin, Linux, macOS, seL4);
			-- HAMR::HW => (x86, amd64, QEMU);

			-- HAMR::Default_Bit_Width => 32;
			-- HAMR::Default_Max_Sequence_Size => 1;
			-- HAMR::Max_String_Size => 256;

			-- HAMR::Bit_Codec_Raw_Connections => true;
	end PFC_Sys.Impl;

	process PFC_Proc
	end PFC_Proc;

	process implementation PFC_Proc.Impl
		subcomponents
			producer: thread Producer;
			filter: thread Filter;
			consumer: thread Consumer;
		connections
			c1_data: port producer.to_filter_data -> filter.from_producer_data;
			c1_event: port producer.to_filter_event -> filter.from_producer_event;
			c2: port filter.to_consumer -> consumer.from_filter;
	end PFC_Proc.Impl;

	thread Producer
		features
			--to_filter: out event data port Mission;
			to_filter_data: out data port Mission;
			to_filter_event: out event port; 
		properties
			Thread_Properties::Dispatch_Protocol => Periodic;
			Timing_Properties::Period => 1000ms;

			Stack_Size => 61440 Bytes;
	end Producer;

	thread implementation Producer.Impl
	end Producer.Impl;

	thread Filter
		features
			from_producer_data: in data port Mission;
			from_producer_event: in event port;
			to_consumer: out event data port Mission;
		properties
			Dispatch_Protocol => Periodic;
			Period => 1000ms;

			Stack_Size => 61440 Bytes;
	end Filter;

	thread implementation Filter.Impl
	end Filter.Impl;

	thread Consumer
		features
			from_filter: in event data port Mission;
		properties
			Dispatch_Protocol => Sporadic;

			Stack_Size => 61440 Bytes;
			Communication_Properties::Queue_Size => 1;
	end Consumer;

	thread implementation Consumer.Impl
	end Consumer.Impl;
end PFC;