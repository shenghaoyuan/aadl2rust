-- 系统功能：演示事件驱动的数据处理流水线模式
--   - 线程Producer：周期性线程（周期1000ms），定时产生Mission类型数据
--   - 线程Filter：偶发线程，当接收到生产者数据时触发，进行过滤处理
--   - 线程Consumer：偶发线程，当接收到过滤器数据时触发，进行最终消费

-- 系统特点：
-- 1. 采用事件驱动架构：一个周期性生产者触发两个偶发消费者（Filter和Consumer）
-- 2. 所有线程间通信都使用事件数据端口（event data port），确保数据传输与事件触发的同步
-- 3. Filter和Consumer线程配置了队列（Queue_Size => 1），处理可能的并发事件
-- 4. 使用Mission复杂数据结构，包含三个Coordinate类型的数组元素
-- 5. 所有线程都有相同的栈大小配置（60KB）

package PFC
public
	with Base_Types, Data_Model;

	data Coordinate
		properties
			Data_Model::Data_Representation => Struct;
	end Coordinate;

	data implementation Coordinate.Impl
		subcomponents
			latitude: data Base_Types::Integer_32;
			longitude: data Base_Types::Integer_32;
			altitude: data Base_Types::Integer_32;
	end Coordinate.Impl;

	data Mission
		properties
			Data_Model::Data_Representation => Array;
			Data_Model::Base_Type => (classifier (Coordinate.Impl));
			Data_Model::Dimension => (3);

			-- HAMR::Bit_Codec_Spec => "data/Mission.sc";
			-- HAMR::Bit_Codec_Max_Size => 288 Bits;
	end Mission;

	system PFC_Sys
	end PFC_Sys;

	system implementation PFC_Sys.Impl
		subcomponents
			proc: process PFC_Proc.Impl;
		-- properties
		-- 	HAMR::Platform => (JVM, Cygwin, Linux, macOS, seL4);
		-- 	HAMR::HW => (x86, amd64, QEMU);

		-- 	HAMR::Default_Bit_Width => 32;
		-- 	HAMR::Default_Max_Sequence_Size => 1;
		-- 	HAMR::Max_String_Size => 256;

		-- 	HAMR::Bit_Codec_Raw_Connections => true;
	end PFC_Sys.Impl;

	process PFC_Proc
	end PFC_Proc;

	process implementation PFC_Proc.Impl
		subcomponents
			producer: thread Producer;
			filter: thread Filter;
			consumer: thread Consumer;
		connections
			c1: port producer.to_filter -> filter.from_producer;
			c2: port filter.to_consumer -> consumer.from_filter;
	end PFC_Proc.Impl;

	thread Producer
		features
			to_filter: out event data port Mission;
		properties
			Thread_Properties::Dispatch_Protocol => Periodic;
			Timing_Properties::Period => 1000ms;

			Stack_Size => 61440 Bytes;
	end Producer;

	thread implementation Producer.Impl
	end Producer.Impl;

	thread Filter
		features
			from_producer: in event data port Mission;
			to_consumer: out event data port Mission;
		properties
			Dispatch_Protocol => Sporadic;

			Stack_Size => 61440 Bytes;
			Communication_Properties::Queue_Size => 1;
	end Filter;

	thread implementation Filter.Impl
	end Filter.Impl;

	thread Consumer
		features
			from_filter: in event data port Mission;
		properties
			Dispatch_Protocol => Sporadic;

			Stack_Size => 61440 Bytes;
			Communication_Properties::Queue_Size => 1;
	end Consumer;

	thread implementation Consumer.Impl
	end Consumer.Impl;
end PFC;