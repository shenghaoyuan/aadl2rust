-- 系统功能：模拟完整的卫星在轨软件系统，包含系统管理、姿态控制、平台管理、有效载荷和数据处理
--   - 系统管理任务：SYS_CYCL（125ms，优先级249），负责总体系统管理
--   - AOCS任务：AOCS_CYCL（125ms，248）、AOCS_CONF（250ms，245）、AOCS_MAN（250ms，244），负责姿态与轨道控制
--   - 平台任务：PF_CYCL（125ms，246）、PF_CONF（250ms，243），负责平台管理
--   - 有效载荷任务：PL_CYCL（125ms，247）、PL_CONF（250ms，242），负责有效载荷操作
--   - 数据处理任务：RTC_HDLR（250ms，255）等6个DHS任务，负责遥测、遥控和数据总线处理

-- 系统特点：
-- 1. 完整的卫星软件系统架构，包含18个线程和6个共享数据组件，形成复杂的多级控制体系
-- 2. 多速率周期性调度，线程周期从125ms到250ms，优先级从255（最高）到242（最低）
-- 3. 使用多种通信机制：事件端口、数据端口、事件数据端口和共享数据访问
-- 4. 基于ERC32处理器（航天领域专用），适合星载计算机系统

package satellite
public

with satellite_hardware;

 -------------------------------------------------------------------------------
  --  the system
  -------------------------------------------------------------------------------

  system OBMU
  end OBMU;

  system implementation OBMU.Impl
    subcomponents
      OBSW: process SAT_APPLI.Impl;
      ERC32: processor satellite_hardware::ERC32.impl;
      -- RAM: memory satellite_hardware::RAM;
      -- AVB: bus satellite_hardware::bus_1553;
      -- ICB: bus satellite_hardware::bus_1553;
      -- PLB: bus satellite_hardware::bus_1553;
         
    -- connections
    --   C1: bus access AVB -> RAM.AVB_bus_connected;
    --   C2: bus access AVB -> ERC32.AVB_bus_connected;
    --   C3: bus access ICB -> RAM.ICB_bus_connected;
    --   C4: bus access ICB -> ERC32.ICB_bus_connected;
    --   C5: bus access PLB -> RAM.PLB_bus_connected;
    --   C6: bus access PLB -> ERC32.PLB_bus_connected;
         
    properties
      Actual_Processor_Binding =>  reference (ERC32) applies to OBSW;
      -- Actual_Memory_Binding =>  (reference (ram)) applies to OBSW;
      
  end OBMU.Impl;



  -------------------------------------------------------------------------------
  --  the on-board software process
  -------------------------------------------------------------------------------
  process SAT_APPLI
  end SAT_APPLI;

    process implementation SAT_APPLI.Impl
    subcomponents
	  -- System Management Tasks
      APPLI_SYS_CYCL: thread SYS_CYCL.impl;
      -- AOCS Tasks
      APPLI_AOCS_CYCL: thread AOCS_CYCL.impl;
      APPLI_AOCS_CONF: thread AOCS_CONF.impl;
      APPLI_AOCS_MAN: thread AOCS_MAN.impl;
      -- Platform Tasks
      APPLI_PF_CYCL: thread PF_CYCL.impl;
      APPLI_PF_CONF: thread PF_CONF.impl;
      -- Payload Tasks
      APPLI_PL_CYCL: thread PL_CYCL.impl;
      APPLI_PL_CONF: thread PL_CONF.impl;
      -- Data Handling System Tasks
      APPLI_RTC_HDLR: thread RTC_HDLR.impl;
      APPLI_TC_HDLR: thread TC_HDLR.impl;
      APPLI_RTM_HDLR: thread RTM_HDLR.impl;
      APPLI_PLB_HDLR: thread PLB_HDLR.impl;
      APPLI_AVB_HDLR: thread AVB_HDLR.impl;
 	    APPLI_ICB_HDLR: thread ICB_HDLR.impl;
      -- Synchronizations with buses
      APPLI_AVB_AOCS_ACQ : data AVB_AOCS_ACQ.impl;
      APPLI_ICB_AOCS_ACQ : data ICB_AOCS_ACQ.impl;
      APPLI_ICB_SYS_ACQ : data ICB_SYS_ACQ.impl;
      APPLI_ICB_PF_ACQ : data ICB_PF_ACQ.impl;
      APPLI_PLB_PF_ACQ : data PLB_PF_ACQ.impl;
      APPLI_PLB_PL_ACQ : data PLB_PL_ACQ.impl;

	connections
   ------------------ (RTC_HDLR) --------------
      C1: port APPLI_RTC_HDLR.SYS_RTC -> APPLI_SYS_CYCL.RTC;
      C2: port APPLI_RTC_HDLR.AOCS_RTC -> APPLI_AOCS_CYCL.RTC;
      C3: port APPLI_RTC_HDLR.PF_RTC -> APPLI_PF_CYCL.RTC;
      C4: port APPLI_RTC_HDLR.PL_RTC -> APPLI_PL_CYCL.RTC;
   ------------------ (TC_HDLR) --------------
      C5: port APPLI_TC_HDLR.SYS_TC -> APPLI_SYS_CYCL.TC;
      C6: port APPLI_TC_HDLR.AOCS_TC -> APPLI_AOCS_CYCL.TC;
      C7: port APPLI_TC_HDLR.PF_TC -> APPLI_PF_CYCL.TC;
      C8: port APPLI_TC_HDLR.PL_TC -> APPLI_PL_CYCL.TC;
  ------------------ (RTTM_HDLR) --------------
      C9: port APPLI_SYS_CYCL.RTM -> APPLI_RTM_HDLR.SYS_RTM;
      C10: port APPLI_AOCS_CYCL.RTM -> APPLI_RTM_HDLR.AOCS_RTM;
      C11: port APPLI_PF_CYCL.RTM -> APPLI_RTM_HDLR.PF_RTM;
      C12: port APPLI_PL_CYCL.RTM -> APPLI_RTM_HDLR.PL_RTM;
    ------------------ (AVB_HDLR) --------------
    	-- ACQ
      C13: data access APPLI_AVB_AOCS_ACQ -> APPLI_AVB_HDLR.AVB_AOCS_ACQ_features;
      C14: data access APPLI_AVB_AOCS_ACQ -> APPLI_AOCS_CYCL.AVB_AOCS_ACQ_features;
      -- AVB event
      C15: port APPLI_AVB_HDLR.AOCS_AVB -> APPLI_AOCS_CYCL.AVB;
     ------------------ (ICB_HDLR) --------------
    	-- ACQs
   	  -- C16: data access APPLI_ICB_SYS_ACQ -> APPLI_ICB_HDLR.ICB_SYS_ACQ_features;
      C17: data access APPLI_ICB_SYS_ACQ -> APPLI_SYS_CYCL.ICB_SYS_ACQ_features;
      C18: data access APPLI_ICB_AOCS_ACQ -> APPLI_ICB_HDLR.ICB_AOCS_ACQ_features;
      -- C19: data access APPLI_ICB_AOCS_ACQ -> APPLI_AOCS_CYCL.ICB_AOCS_ACQ_features;
      C20: data access APPLI_ICB_PF_ACQ -> APPLI_PF_CYCL.ICB_PF_ACQ_features;
      -- C21: data access APPLI_ICB_PF_ACQ -> APPLI_ICB_HDLR.ICB_PF_ACQ_features;
		-- ICB events
      C22: port APPLI_ICB_HDLR.SYS_ICB -> APPLI_SYS_CYCL.ICB;
	    C23: port APPLI_ICB_HDLR.AOCS_ICB -> APPLI_AOCS_CYCL.ICB;
      C24: port APPLI_ICB_HDLR.PF_ICB -> APPLI_PF_CYCL.ICB;
      ------------------ (PLB_HDLR) --------------
    	-- ACQ
      -- C25: data access APPLI_PLB_PF_ACQ -> APPLI_PF_CYCL.PLB_PF_ACQ_features;
      C26: data access APPLI_PLB_PF_ACQ -> APPLI_PLB_HDLR.PLB_PF_ACQ_features;
      C27: data access APPLI_PLB_PL_ACQ -> APPLI_PL_CYCL.PLB_PL_ACQ_features;
      -- C28: data access APPLI_PLB_PL_ACQ -> APPLI_PLB_HDLR.PLB_PL_ACQ_features;
      	-- events
      C29: port APPLI_PLB_HDLR.PF_PLB -> APPLI_PF_CYCL.PLB;
      C30: port APPLI_PLB_HDLR.PL_PLB -> APPLI_PL_CYCL.PLB;
      ------ (trigger asynchronous AOCS tasks) -------
      C31: port APPLI_AOCS_CYCL.CONF_ENABLE -> APPLI_AOCS_CONF.ENABLE;
      C32: port APPLI_AOCS_CYCL.MAN_ENABLE -> APPLI_AOCS_MAN.ENABLE;
      ------ (trigger asynchronous Platform tasks ) --
      C33: port APPLI_PF_CYCL.CONF_ENABLE -> APPLI_PF_CONF.ENABLE;
      ------ (trigger asynchronous Payload tasks) -----
      C34: port APPLI_PL_CYCL.CONF_ENABLE -> APPLI_PL_CONF.ENABLE;
      -------------- (system management) ----------------------
      C35: port APPLI_AOCS_CYCL.SYS -> APPLI_SYS_CYCL.AOCS;
      C36: port APPLI_PF_CYCL.SYS -> APPLI_SYS_CYCL.PF;
      C37: port APPLI_PL_CYCL.SYS -> APPLI_SYS_CYCL.PL;

  end SAT_APPLI.Impl;

  -----------------------------------------------------------
  -- SYSTEM MANAGEMENT function                            --
  -----------------------------------------------------------

  thread SYS_CYCL
    features
      -- Synchronizations with bus
	    ICB_SYS_ACQ_features : requires data access ICB_SYS_ACQ;
      -- TC receiving
      TC: in event data port default_type;
      -- RTC event
      RTC: in event port;
      -- RTM transmitting
 	    RTM : out event data port default_type;
       -- ICB event
      ICB: in event port;
      -- AOCS comm
      AOCS: in event port;
      -- platform comm
      PF: in event port;
      -- payload comm
      PL: in event port;
    properties
      Dispatch_Protocol => Periodic;
      Period => 125 ms;
      Deadline => 125 ms;
      Compute_Execution_Time => 5 ms..5 ms ;
      Priority => 249;
  end SYS_CYCL;

  thread implementation SYS_CYCL.impl
  end SYS_CYCL.impl;

  -----------------------------------------------------------
  -- DHS tasks
  -----------------------------------------------------------
  thread RTC_HDLR
    features
     AOCS_RTC : out event port;
     PF_RTC : out event port;
     PL_RTC : out event port;
     SYS_RTC : out event port;
  end RTC_HDLR;

  thread implementation RTC_HDLR.impl
  properties
      Dispatch_Protocol => Periodic;
      Period => 250 ms;
      Deadline => 250 ms;
	    Compute_Execution_Time => 1 ms..1 ms ;
	    Priority => 255;
  end RTC_HDLR.impl;

  thread TC_HDLR
  features
      AOCS_TC : out event data port default_type;
      PF_TC : out event data port default_type;
      PL_TC : out event data port default_type;
      SYS_TC : out event data port default_type;
  end TC_HDLR;

  thread implementation TC_HDLR.impl
  properties
      Dispatch_Protocol => Periodic;
      Period => 250 ms;
      Deadline => 250 ms;
      Compute_Execution_Time => 2 ms..2 ms ;
      Priority => 254;
  end TC_HDLR.impl;

  thread RTM_HDLR
  features
  	AOCS_RTM : in event data port default_type;
  	PF_RTM : in event data port default_type;
  	PL_RTM : in event data port default_type;
  	SYS_RTM : in event data port default_type;
  end RTM_HDLR;

  thread implementation RTM_HDLR.impl
  properties
    Dispatch_Protocol => Periodic;
    Period => 250 ms;
    Deadline => 250 ms;
    Compute_Execution_Time => 3 ms..4 ms ;
    Priority => 250;
  end RTM_HDLR.impl;

  thread AVB_HDLR
    features
      -- Synchronizations
      AVB_AOCS_ACQ_features : requires data access AVB_AOCS_ACQ;
      AOCS_AVB : out event port;
  end AVB_HDLR;

  thread implementation AVB_HDLR.impl
  properties
      Dispatch_Protocol => Periodic;
      Period => 125 ms;
      Deadline => 125 ms;
      Compute_Execution_Time => 0 ms.. 3 ms;
      Priority => 253;
  end AVB_HDLR.impl;

  thread ICB_HDLR
    features
	  ICB_AOCS_ACQ_features : requires data access ICB_AOCS_ACQ;
	  -- ICB_PF_ACQ_features : requires data access ICB_PF_ACQ;
	  -- ICB_SYS_ACQ_features : requires data access ICB_SYS_ACQ;
      AOCS_ICB : out event port;
      PF_ICB : out event port;
      SYS_ICB : out event port;
  end ICB_HDLR;

  thread implementation ICB_HDLR.impl
  properties
      Dispatch_Protocol => Periodic;
      Period => 125 ms;
      Deadline => 125 ms;
      Compute_Execution_Time => 3 ms..4 ms ;
      Priority => 252;
  end ICB_HDLR.impl;

  thread PLB_HDLR
    features
	  PLB_PF_ACQ_features : requires data access PLB_PF_ACQ;
	  -- PLB_PL_ACQ_features : requires data access PLB_PL_ACQ;
      PF_PLB : out event port;
      PL_PLB : out event port;
    
  end PLB_HDLR;

  thread implementation PLB_HDLR.impl
  properties
      Dispatch_Protocol => Periodic;
      Period => 125 ms;
      Deadline => 125 ms;
      Compute_Execution_Time => 6 ms..7 ms ;
      Priority => 251;
  end PLB_HDLR.impl;

  -----------------------------------------------------------
  -- AOCS function                                         --
  -----------------------------------------------------------

  thread AOCS_CYCL
    features
      -- Synchronizations with bus
      AVB_AOCS_ACQ_features : requires data access AVB_AOCS_ACQ;
      -- ICB_AOCS_ACQ_features : requires data access ICB_AOCS_ACQ;
      -- Synchronizations with asynchronous AOCS appli tasks
      CONF_ENABLE: out event port;
      MAN_ENABLE: out event port;
      -- TC receiving
      TC: in event data port default_type;
      -- RTC event
      RTC: in event port;
      -- RTM transmitting
 	    RTM : out event data port default_type;
       -- AVB event
      AVB: in event port;
       -- ICB event
      ICB: in event port;
      --system comm
      SYS: out event port;
  end AOCS_CYCL;

  thread implementation AOCS_CYCL.impl
  properties
      Dispatch_Protocol => Periodic;
      Period => 125 ms;
      Deadline => 125 ms;
      Compute_Execution_Time => 25 ms..25 ms ;
      Priority => 248;
  end AOCS_CYCL.impl;

  thread AOCS_CONF
    features
      ENABLE: in event port;
  end AOCS_CONF;

  thread implementation AOCS_CONF.impl
  properties
--      Dispatch_Protocol => Sporadic;
      Dispatch_Protocol => Periodic;
      Period => 250 ms;
      Deadline => 250 ms;
      Compute_Execution_Time => 11 ms..60 ms ;
      Priority => 245;
  end AOCS_CONF.impl;

  thread AOCS_MAN
    features
      ENABLE: in event port;
  end AOCS_MAN;

  thread implementation AOCS_MAN.impl
  properties
--      Dispatch_Protocol => Sporadic;
      Dispatch_Protocol => Periodic;
      Period => 250 ms;
      Deadline => 250 ms;
      Compute_Execution_Time => 11 ms..12 ms ;
      Priority => 244;
  end AOCS_MAN.impl;

  -----------------------------------------------------------
  -- PLATFORM functions                                    --
  -----------------------------------------------------------

  thread PF_CYCL
    features
      -- Synchronizations with bus
	  ICB_PF_ACQ_features : requires data access ICB_PF_ACQ;
	  -- PLB_PF_ACQ_features : requires data access PLB_PF_ACQ;
      -- Synchronizations with asynchronous PL appli tasks
      CONF_ENABLE: out event port;
      -- TC receiving
      TC: in event data port default_type;
      -- RTC event
      RTC: in event port;
      -- RTM transmitting
 	  RTM : out event data port default_type;
       -- ICB event
      ICB: in event port;
       -- PLB event
      PLB: in event port;
      --system comm
      SYS: out event port;
  end PF_CYCL;

  thread implementation PF_CYCL.impl
  properties
      Dispatch_Protocol => Periodic;
      Period => 125 ms;
      Deadline => 125 ms;
      Compute_Execution_Time => 4 ms..4 ms ;
      Priority => 246;
  end PF_CYCL.impl;

  thread PF_CONF
    features
      ENABLE: in event port;
  end PF_CONF;

  thread implementation PF_CONF.impl
  properties
--      Dispatch_Protocol => Sporadic;
      Dispatch_Protocol => Periodic;
      Period => 250 ms;
      Deadline => 250 ms;
      Compute_Execution_Time => 8 ms..8 ms ;
      Priority => 243;
  end PF_CONF.impl;

  -----------------------------------------------------------
  -- PAYLOAD functions                                    --
  -----------------------------------------------------------

  thread PL_CYCL
    features
      -- Synchronizations with bus
	  PLB_PL_ACQ_features : requires data access PLB_PL_ACQ;
      -- Synchronizations with asynchronous PL appli tasks
      CONF_ENABLE: out event port;
      -- TC receiving
      TC: in event data port default_type;
      -- RTC event
      RTC: in event port;
      -- RTM transmitting
 	  RTM : out event data port default_type;
      -- PLB event
      PLB: in event port;
      --system comm
      SYS: out event port;
  end PL_CYCL;

  thread implementation PL_CYCL.impl
  properties
      Dispatch_Protocol => Periodic;
      Period => 125 ms;
      Deadline => 125 ms;
      Compute_Execution_Time => 8 ms..8 ms ;
      Priority => 247;
  end PL_CYCL.impl;

  thread PL_CONF
    features
      ENABLE: in event port;
  end PL_CONF;

  thread implementation PL_CONF.impl
  properties
--      Dispatch_Protocol => Sporadic;
      Dispatch_Protocol => Periodic;
      Period => 250 ms;
      Deadline => 250 ms;
      Compute_Execution_Time => 8 ms..8 ms ;
     Priority => 242;
  end PL_CONF.impl;

--------------------------------------------
------ DATA
-------------------------------------------
	data default_type
  properties
    Data_Model::Data_Representation => Integer; 
  end default_type;

	data AVB_AOCS_ACQ
	properties
	  Data_Size => 620 Bits;
    Data_Model::Data_Representation => Struct;
	end AVB_AOCS_ACQ;

  data implementation AVB_AOCS_ACQ.impl
    subcomponents
        val : data Base_Types::Integer_64;
  end AVB_AOCS_ACQ.impl;

	data ICB_AOCS_ACQ
	properties
	  Data_Size => 620 Bits;
    Data_Model::Data_Representation => Struct;
	end ICB_AOCS_ACQ;

  data implementation ICB_AOCS_ACQ.impl
    subcomponents
        val : data Base_Types::Integer_64;
  end ICB_AOCS_ACQ.impl;

	data ICB_SYS_ACQ
	properties
	  Data_Size => 620 Bits;
    Data_Model::Data_Representation => Struct;
	end ICB_SYS_ACQ;

  data implementation ICB_SYS_ACQ.impl
    subcomponents
        val : data Base_Types::Integer_64;
  end ICB_SYS_ACQ.impl;

	data ICB_PF_ACQ
	properties
	  Data_Size => 620 Bits;
    Data_Model::Data_Representation => Struct;
	end ICB_PF_ACQ;

  data implementation ICB_PF_ACQ.impl
    subcomponents
        val : data Base_Types::Integer_64;
  end ICB_PF_ACQ.impl;

	data PLB_PF_ACQ
	properties
	  Data_Size => 620 Bits;
    Data_Model::Data_Representation => Struct;
	end PLB_PF_ACQ;

  data implementation PLB_PF_ACQ.impl
    subcomponents
        val : data Base_Types::Integer_64;
  end PLB_PF_ACQ.impl;

	data PLB_PL_ACQ
	properties
	  Data_Size => 620 Bits;
    Data_Model::Data_Representation => Struct;
	end PLB_PL_ACQ;

  data implementation PLB_PL_ACQ.impl
    subcomponents
        val : data Base_Types::Integer_64;
  end PLB_PL_ACQ.impl;

end satellite;
