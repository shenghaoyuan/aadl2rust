-- 同testdpmon案例（调度协议：Periodic）
package testdpmon
public
	-- with HAMR;
	with Base_Types;
	
	thread source_t
		features
			write_port: out data port Base_Types::Integer_64;
	end source_t;
	
	thread implementation source_t.impl
	calls 
	Mycalls: {
		Init_Spg : subprogram source_init;
		A_Spg : subprogram C_A;
	};
	connections
		cnx_a : parameter A_Spg.out_param -> write_port;
	properties
			Dispatch_Protocol => Periodic;
			Period => 1000 ms;
			Priority => 1;
			-- Source_Text => ("behavior_code/components/source/src/source.c");
			-- Initialize_Entrypoint_Source_Text => "testdpmon_source_component_init";
			-- Compute_Entrypoint_Source_Text => "testdpmon_source_component_time_triggered";
	end source_t.impl;
	
	thread destination_t
		features
			read_port: in data port Base_Types::Integer_64;
	end destination_t;
	
	thread implementation destination_t.impl
	calls 
	Mycalls: {
		B_Spg : subprogram C_B;
	};
	connections
		cnx_b : parameter read_port -> B_Spg.in_param;
	properties
			Dispatch_Protocol => Periodic;
			Period => 2000 ms;
			Priority => 1;
			-- Source_Text => ("behavior_code/components/destination/src/destination.c");
			-- Compute_Entrypoint_Source_Text => "testdpmon_destination_component_time_triggered";
	end destination_t.impl;

	processor proc
	end proc;

	processor implementation proc.impl
	end proc.impl;

	process top_process
	end top_process;

	process implementation top_process.impl
		subcomponents
			src: thread source_t.impl;
			dest: thread destination_t.impl;
		connections
 			conn1: port src.write_port -> dest.read_port;
	end top_process.impl;

	system top
	end top;
	
	system implementation top.impl
		subcomponents
			proc: processor proc.impl;
			testdpmon: process top_process.impl;
		properties
			Actual_Processor_Binding => reference (proc) applies to testdpmon;
			-- HAMR::Platform => (seL4_TB, seL4_Only);
		-- annex resolute {**
		-- 	check CASE_Tools
		-- **};
	end top.impl;
	
	subprogram source_init
	properties
		source_language => (C);
		source_name => "testdpmon_source_component_init";
		source_text => ("source.c");
	end source_init;

	subprogram C_A
		features
			out_param : out parameter Base_Types::Integer_64;
		properties
			source_language => (C);
			source_name => "run_sender";
			source_text     => ("source.c");
	end C_A;

	subprogram C_B
		features
			in_param : in parameter Base_Types::Integer_64;
		properties
			source_language => (C);
			source_name => "run_receiver";
			source_text     => ("source.c");
	end C_B;
end testdpmon;
