-- 系统功能概述：
-- 这是一个Lua脚本集成测试案例，展示了如何在AADL系统中集成和调用Lua脚本。
-- 系统包含两个线程，分别以不同周期调用C语言函数，其中一个函数会调用Lua脚本。
--
-- 线程功能：
-- 1. Task1线程（周期1000ms，优先级1）：周期性调用hello_func函数，打印"hello world"
-- 2. Task2线程（周期500ms，优先级2）：周期性调用lua_sample函数，该函数执行Lua脚本
--
-- 系统特点：
-- 1. 这是Lua脚本集成测试案例，展示了如何在嵌入式实时系统中使用脚本语言
-- 2. 双线程、双速率调度（1000ms和500ms），展示了不同优先级的任务调度
-- 3. Task2的优先级(2)高于Task1(1)，但Task2周期更短，展示了优先级调度策略
-- 4. 使用POSIX最高优先级优先调度协议
-- 5. 计算执行时间范围明确指定（0-3ms），便于实时性分析
package test_lua
public
  -- with Deployment;
  with Data_Model;

  -----------------
  -- Subprograms --
  -----------------

  subprogram Hello_Spg_1
  properties
    source_language => (C);
    source_text     => ("hello.c");
    source_name     => "hello_func";
  end Hello_Spg_1;

  subprogram Hello_Spg_2
  properties
    source_language => (C);
    source_text     => ("hello.c");
    source_name     => "lua_sample";
  end Hello_Spg_2;

  -------------
  -- Threads --
  -------------

  thread Task1
  end Task1;

  thread implementation Task1.impl_1
  calls
    Mycalls: {
      P_Spg : subprogram Hello_Spg_1;
    };
  properties
    Dispatch_Protocol                  => Periodic;
    Period                             => 1000ms;
    Compute_Execution_time             => 0 ms .. 3 ms;
    Deadline                           => 1000 ms;
    Priority => 1;
  end Task1.impl_1;

  thread Task2
  end Task2;

  thread implementation Task2.impl_2
  calls
    Mycalls: {
      P_Spg : subprogram Hello_Spg_2;
    };
  properties
    Dispatch_Protocol                  => Periodic;
    Period                             => 500ms;
    Compute_Execution_time             => 0 ms .. 3 ms;
    Deadline                           => 500 ms;
    Priority => 2;
  end Task2.impl_2;

  ---------------
  -- Processor --
  ---------------

  processor cpurm
  -- properties
  --   Deployment::Execution_Platform => (linux32);
  end cpurm;

  processor implementation cpurm.impl
  properties
    Scheduling_Protocol => (Posix_1003_Highest_Priority_First_Protocol);
    Priority_Range => 0 .. 255;
  end cpurm.impl;

  ---------------
  -- Processes --
  ---------------

  process node_a
  end node_a;

  process implementation node_a.impl
  subcomponents
    Task1 : thread Task1.impl_1;
    Task2 : thread Task2.impl_2;
  end node_a.impl;

  ------------
  -- System --
  ------------

  system lua
  end lua;

  system implementation lua.impl
  subcomponents
    node_a : process node_a.impl;
    cpurm : processor cpurm.impl;
  properties
    Actual_Processor_Binding => reference (cpurm) applies to node_a;
  end lua.impl;

end test_lua;
