package drone 
public


	----------------------------------- ROS Communication mechanism -----------------------------------
	
	-- ROS 1 generic interface ----------------------------------------------------------------
	-- virtual bus ROS1

	-- end ROS1;
	
	-- -- ROS 1 Noetic implementation ------------------------------------------------------------
	-- -- This virtual bus can be used to instantiate a ROS1 topic.
	-- -- The topic created can be bind to one or more connections.
	-- virtual bus implementation ROS1.Noetic
	-- 	properties
	-- 		Transmission_Time => [Fixed => 100 ns .. 500 ns; PerByte => 5 ns .. 6 ns;];
	-- end ROS1.Noetic;
	
	---------------------------------------------------------------------------------------------------
	
	
	----------------------------------- Companion Computer Processor ---------------------------------- 
	processor DroneCompanionComputer
		properties
			Scheduling_Protocol => (POSIX_1003_HIGHEST_PRIORITY_FIRST_PROTOCOL);	
	end DroneCompanionComputer;
	
	---------------------------------------------------------------------------------------------------


	--------------------------------------- Flight System Device --------------------------------------

	device FlightSystem
		features
			nextDirection: in data port;  
		-- flows
		-- 	ndf: flow sink nextDirection; -- {Latency => 15ms .. 50ms;};
		properties
			Dispatch_Protocol => Periodic;
			Period => 100ms;
	end FlightSystem;

	---------------------------------------------------------------------------------------------------


	------------------------------------------ Sensor devices -----------------------------------------
	
	-- Thermal Camera ---------------------------------------------------------------------
	-- A thermal camera outputs a visual image that shows temperature variations in an area. 
	-- This image is created by converting infrared radiation emitted by objects into visible light.
	-- The image created have a dimension of N x M pixels and a sample rate from 20 FPS to 40 FPS.
	device ThermalCamera
		features
			image: out data port;
			-- It is important to put the "Output_Time" parameter?
			--image: out data port {Output_Time => ([Time => Deadline; Offset => 0ms .. 0ms;]);};
		-- flows
		-- 	-- Latency is necessary if we doesnt consider the latency in process execution time of the processes involved in communication.
		-- 	-- Because the reading thread will be blocked in its activation period while data is not read (maximum value = 50 ms)
		-- 	imageGenerationFlow: flow source image; -- {Latency => 25ms .. 50ms;};
		properties
			Dispatch_Protocol => Periodic;
			-- The same as for latency time
			Period => 50ms; 
	end ThermalCamera;
	
	-- Temperature sensor ------------------------------------------------------------------
	-- The temperature sensor (different from the thermostat) generates an 
	-- analog output signal proportional to the measured temperature in binary format.
	-- Thermostat is a device that output a boolean flag indicates temperature goes high than a pre-defined threshold.
	-- It has a high sample time compared to other solutions. 
	device TempSensor
		features
			temp: out data port; -- A integer number that represents the analog signal measured.
			highTempFlag: out event port; -- output flag indicating that threshold temperature was reached.
		-- flows
		-- 	tempMeasureFlow: flow source temp {Latency => 10ms .. 15ms;}; -- Latency between temperature measured by the sensor, converted and sent to this output.
		-- 	highTempMeasureFlow: flow source highTempFlag {Latency => 10ms .. 15ms;};
		properties 
			Dispatch_Protocol => Periodic; -- The periodic behavior is important when to analyze latency, because in sampling communication this period can be account as receiver can lost a sample.
			Period => 1000ms; -- The period is equal the maximum latency.
	end TempSensor;
	
	---------------------------------------------------------------------------------------------------


	---------------------------------------- Thermal Image Tasks --------------------------------------

	-- Thermal Camera Thread -----------------------------------------------------------------
	thread ThermalImageProcessing
	features
		image: in data port; -- Image read from I2C interface.
		color: out data port;
	-- flows
	-- 	im2col: flow path image -> color;
	properties
		Dispatch_Protocol => Periodic;
		Period => 1000 ms;
		Deadline => 1000 ms;
		Compute_Execution_Time => 200ms .. 300ms;
	end ThermalImageProcessing;

	-- Thermal Camera Process -----------------------------------------------------------------
	process ThermalImageProcessingProc
	features
		image: in data port; -- Image read from I2C interface.
		--color: out data port {Output_Time => ([Time => Deadline; Offset => 0ms .. 0ms;]);};
		--color: out event data port {Data_Size => 4 Bytes;}; -- Using data size in port is not counted in bus transmission time.
		color: out event data port;
	-- flows
	-- 	im2col: flow path image -> color;
	end ThermalImageProcessingProc;

	-- Thermal Camera Process Implementation ----------------------------------------
	process implementation ThermalImageProcessingProc.impl
	subcomponents
		node: thread ThermalImageProcessing;
	connections
		c1: port image -> node.image ;
		c2: port node.color -> color ;
	-- flows
	-- 	im2col: flow path image -> c1 -> node.im2col -> c2 -> color;
	end ThermalImageProcessingProc.impl;
	
	---------------------------------------------------------------------------------------------------


	-------------------------------------- Temperature Sensor Tasks -----------------------------------
	
	
	-- Temperature Sensor Acquiring Node ---------------------------------------------
	thread TempSensorAcquiringNode
        features
			temp_in: in data port; -- Temperature sensor read from I2C interface.
			temp_out: out data port; -- Temperature sensor published in ROS topic.
		-- flows
		-- 	tempAcqui: flow path temp_in -> temp_out;
		properties
			Dispatch_Protocol => Periodic;
			Period => 200 ms; -- Period is equal to the minimum sample time of the temperature sensor because it will be dispatched by the receive flag.
			Compute_Execution_Time => 100 us .. 200 us;
    end TempSensorAcquiringNode;
    
    
    -- Temperature Sensor Acquiring Process -------------------------------------------
	process TempSensorAcquiring
	features
		temp_in: in data port; -- Image read from I2C interface.
		--temp_out: out data port {Output_Time => ([Time => Deadline; Offset => 0ms .. 0ms;]);};
		temp_out: out event data port;
	-- flows
	-- 	tempAcqui: flow path temp_in -> temp_out;
    end TempSensorAcquiring;


	-- Temperature Sensor Acquiring Process Implementation ----------------------------
	process implementation TempSensorAcquiring.impl
		subcomponents
			node: thread TempSensorAcquiringNode;
		connections
			c1: port temp_in -> node.temp_in;
			c2: port  node.temp_out -> temp_out;
		-- flows
		-- 	tempAcqui: flow path temp_in -> c1 -> node.tempAcqui -> c2 -> temp_out;
	end TempSensorAcquiring.impl;

	---------------------------------------------------------------------------------------------------
	
	
	---------------------------------------- Drone Agent Tasks ----------------------------------------

	-- High temperature flag thread ----------------------------------------------------
	-- This thread will be probably be implemented as a callback created by the user and 
	-- launched by OS by means of a thread handler.
	-- As a callback it will invoked on demand of the temperature flag IRQ.
	thread HighTempFlagThread
		features
			highTempFlag_in: in event port; -- thermostate flag read from IRQ interface.
			highTempFlag_out: out event port; -- thermostate published in ROS topic.
		-- flows
		-- 	tempFlag2Agent: flow path highTempFlag_in -> highTempFlag_out;
		properties
			Dispatch_Protocol => Sporadic;
			Period => 1 ms; -- Although sensor speed is about 100 ns to 1000 ns, 1 ms is suficient for response time.
			Deadline => 1 ms;
			Compute_Execution_Time => 100 us .. 200 us;
	end HighTempFlagThread;

	-- Drone Agent Thread ----------------------------------------------------
	-- This thread will handle the "intelligence" of the drone, performing
	-- decision making about the internal state e controlling the the 
	-- flight system. 
	thread DroneAgentThread
	features
		ctrlOutput: out data port; -- Drone actuator speeds sent to flight controller via UART interface.
		highTempFlag_in: in event port; -- thermostate flag read from shared memory.
		thermalImageColor: in event data port;
		temp_in: in event data port;
	-- flows
	-- 	tempFlag2dir: flow path highTempFlag_in -> ctrlOutput;
	-- 	tempValue2dir: flow path temp_in -> ctrlOutput;
	-- 	ImgColor2dir: flow path thermalImageColor -> ctrlOutput;
	properties
		Dispatch_Protocol => Aperiodic;
		Deadline => 1 ms;
		Compute_Execution_Time => 100us .. 200us;
end DroneAgentThread;

	-- Drone Agent Process ---------------------------------------------------
	process DroneAgent
	features
		--thermalImageColor: in event data port {Data_Size => 4 Bytes;}; -- A integer number representing the predominant color in the image, read from ROS topic.
		ctrlOutput: out data port; -- Drone actuator speeds sent to flight controller via UART interface.
		highTempFlag_in: in event port; -- thermostate flag read from IRQ interface.
		thermalImageColor: in event data port;
		temp_in: in event data port;
	-- flows
	-- 	im2dir: flow path thermalImageColor -> ctrlOutput;
	-- 	thermo2dir: flow path highTempFlag_in -> ctrlOutput;
	-- 	temp2dir: flow path temp_in -> ctrlOutput;
	end DroneAgent;

	-- Drone Agent Process Implementation ------------------------------------
	process implementation DroneAgent.impl
	subcomponents
		HighTempFlagCallBack: thread HighTempFlagThread;
		AgentThread: thread DroneAgentThread;
	connections
		c1: port temp_in -> AgentThread.temp_in;
		c2: port highTempFlag_in -> HighTempFlagCallBack.highTempFlag_in;
		c3: port HighTempFlagCallBack.highTempFlag_out -> AgentThread.highTempFlag_in; -- The agent thread must awake as soon as possible the heat was detected.
		c4: port AgentThread.ctrlOutput -> ctrlOutput;
		c5: port thermalImageColor -> AgentThread.thermalImageColor;
	-- flows
	-- 	im2dir: flow path thermalImageColor -> c5 -> AgentThread.ImgColor2dir -> c4 -> ctrlOutput;
	-- 	thermo2dir: flow path highTempFlag_in -> c2 -> HighTempFlagCallBack.tempFlag2Agent -> c3 -> AgentThread.tempFlag2dir -> c4 -> ctrlOutput;
	-- 	temp2dir: flow path temp_in -> c1 -> AgentThread.tempValue2dir -> c4 -> ctrlOutput;

	end DroneAgent.impl;

	---------------------------------------------------------------------------------------------------


	---------------------------------------- Drone System ----------------------------------------
	system Drone

	end Drone;

	system implementation Drone.impl
		subcomponents
			--Processors
			jetson: processor DroneCompanionComputer;
			
			--Processes:
			agent: process DroneAgent.impl;
			thermal_image_process: process ThermalImageProcessingProc.impl;
			temp_sensor_acquiring: process TempSensorAcquiring.impl;

			--Devices
			cam: device ThermalCamera;
			tsensor: device TempSensor;
			fsys: device FlightSystem;
			
			--Communication systems
			-- ROS1_ImageColor_Topic: virtual bus ROS1.Noetic {Data_size => 4 Bytes;};
			-- ROS1_SensorTemp_Topic: virtual bus ROS1.Noetic {Data_size => 2 Bytes;};
			
		connections
			c1: port tsensor.highTempFlag -> agent.highTempFlag_in; -- Connection is immediate because a IRQ flag wakes the receiver.
			c2: port temp_sensor_acquiring.temp_out -> agent.temp_in; -- Temperature color through ROS communication mechanism.
			-- Image is read from device through I2C interface. 
			-- The connection in sampling because when task tries to read the image, 
			-- the sensor can be in the stage starting the conversion.
			-- The latency is 1536 bytes per frame x 8 / 1MHz = 12.29 ms
			-- Latency is necessary if we doesnt consider the latency in process execution time of the processes involved in communication.
			c3: port cam.image -> thermal_image_process.image; --{Timing => Immediate; Latency => 12290 us .. 12290 us;}; 
			c4: port thermal_image_process.color -> agent.thermalImageColor;
			c5: port agent.ctrlOutput -> fsys.nextDirection;
			c6: port tsensor.temp -> temp_sensor_acquiring.temp_in;
		-- flows
		-- 	img2dir_flw: end to end flow  cam.imageGenerationFlow -> c3 -> thermal_image_process.im2col -> c4 -> agent.im2dir -> c5 -> fsys.ndf { Latency => 0ms .. 1500ms; }; -- Expected end-to-end latency
		-- 	temp2dir_flw: end to end flow tsensor.tempMeasureFlow -> c6 -> temp_sensor_acquiring.tempAcqui -> c2 -> agent.temp2dir -> c5 -> fsys.ndf { Latency => 0ms .. 1000ms; }; -- Expected end-to-end latency
		-- 	highTemp2dir_flw: end to end flow tsensor.highTempMeasureFlow -> c1 -> agent.thermo2dir -> c5 -> fsys.ndf { Latency => 0ms .. 500ms; }; -- Expected end-to-end latency
		properties
			Actual_Processor_Binding => reference (jetson) applies to agent;
			Actual_Processor_Binding => reference (jetson) applies to thermal_image_process;
			-- Actual_Connection_Binding => (reference (ROS1_ImageColor_Topic)) applies to c4;
			-- Actual_Connection_Binding => (reference (ROS1_SensorTemp_Topic)) applies to c2;
	end Drone.impl;
	
	---------------------------------------------------------------------------------------------------
	
	
end drone;
