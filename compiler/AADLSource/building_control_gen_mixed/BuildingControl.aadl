-- 系统功能：模拟建筑温度监测与风扇控制系统
--   - 线程TempSensor：周期性线程（周期1秒），监测温度变化并输出当前温度
--   - 线程SetPointDriver：周期性线程（周期5秒），产生温度设定点范围
--   - 线程TempControl：偶发线程，根据当前温度与设定点比较，发出风扇控制命令
--   - 线程Fan：偶发线程，接收风扇控制命令并执行，返回执行确认

-- 系统特点：
-- 1. 使用复杂温度数据结构，包含温度值和单位
-- 2. 设定点数据包含高温和低温两个温度范围边界
-- 3. 采用混合触发机制：温度传感器周期性触发，温度控制器和风扇由事件触发
-- 4. 形成完整的控制反馈回路：传感器→控制器→执行器→确认反馈
-- 5. 使用枚举类型表示风扇命令（开/关）和确认状态（正常/错误）

package BuildingControl
public
	with Base_Types, Data_Model;

	system BuildingControlDemo
		-- features
		-- 	setpoint: in event data port SetPoint;
	end BuildingControlDemo;

	system implementation BuildingControlDemo.Impl
		subcomponents
			proc: processor TempControlProcessor.Impl;
			tcp: process TempControlProcess.Impl;
		-- connections
		-- 	sp: port setpoint -> tcp.setpoint;
		properties
			Actual_Processor_Binding => reference (proc) applies to tcp;
		-- annex resolute {**
		-- 	check CASE_Tools
		-- **};

	end BuildingControlDemo.Impl;

	processor TempControlProcessor
	end TempControlProcessor;

	processor implementation TempControlProcessor.Impl
	end TempControlProcessor.Impl;

	process TempControlProcess
		-- features
		-- 	setpoint: in event data port SetPoint;
	end TempControlProcess;

	process implementation TempControlProcess.Impl
		subcomponents
			tempsensor: thread TempSensor.Impl;
			tempcontrol: thread TempControl.Impl;
			fan: thread Fan.Impl;
			driver: thread SetPointDriver.Impl;
		connections
			sp: port driver.setpoint -> tempcontrol.setpoint;
			ct: port tempsensor.currenttemp -> tempcontrol.currenttemp;
			tc: port tempsensor.tempchanged -> tempcontrol.tempchanged;
			fc: port tempcontrol.fancmd -> fan.fancmd;
			fa: port fan.fanack -> tempcontrol.fanack;
	end TempControlProcess.Impl;

	thread TempSensor
		features
			currenttemp: out data port Temperature;
			tempchanged: out event port;
		properties
			Dispatch_Protocol => Periodic;
			Period => 1 sec;
			Stack_Size => 114688 Bytes;
	end TempSensor;

	thread implementation TempSensor.Impl
	end TempSensor.Impl;

	thread Fan
		features
			fancmd: in event data port FanCmd;
			fanack: out event data port FanAck;
		properties
			Dispatch_Protocol => Sporadic;
			Period => 1 sec;
			Stack_Size => 114688 Bytes;			
	end Fan;

	thread implementation Fan.Impl
	end Fan.Impl;

	thread TempControl
		features
			currenttemp: in data port Temperature;
			tempchanged: in event port;
			fanack: in event data port FanAck;
			setpoint: in event data port SetPoint;
			fancmd: out event data port FanCmd;
		properties
			Dispatch_Protocol => Sporadic;
			Period => 1 sec;
			Stack_Size => 114688 Bytes;			
	end TempControl;

	thread implementation TempControl.Impl
	end TempControl.Impl;

	thread SetPointDriver
		features
			setpoint: out event data port SetPoint;
		properties
			Dispatch_Protocol => Periodic;
			Period => 5 sec;
			Stack_Size => 114688 Bytes;
	end SetPointDriver;

	thread implementation SetPointDriver.Impl
	end SetPointDriver.Impl;

	data Temperature
	end Temperature;

	data implementation Temperature.Impl
		subcomponents
			degrees: data Base_Types::Float_32;
			unit: data TempUnit;
	end Temperature.Impl;

	data SetPoint
	end SetPoint;

	data implementation SetPoint.Impl
		subcomponents
			low: data Temperature;
			high: data Temperature;
	end SetPoint.Impl;

	data TempUnit
		properties
			Data_Model::Data_Representation => Enum;
			Data_Model::Enumerators => ("Fahrenheit", "Celsius", "Kelvin");
	end TempUnit;

	data FanAck
		properties
			Data_Model::Data_Representation => Enum;
			Data_Model::Enumerators => ("Ok", "Error");
	end FanAck;

	data FanCmd
		properties
			Data_Model::Data_Representation => Enum;
			Data_Model::Enumerators => ("On", "Off");
	end FanCmd;

end BuildingControl;