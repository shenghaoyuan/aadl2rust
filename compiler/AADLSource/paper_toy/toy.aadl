package Toy_Example_NoWrapper
public

  -- with Deployment;
  with Data_Model,Base_Types;

  ----------
  -- Data --
  ----------

  data POS
  features
    Update: provides subprogram access Update;
  properties
    Data_Model::Data_Representation => Struct;
  end POS;

  data implementation POS.Impl
  subcomponents
    field : data Integer;
    spgUpdate : subprogram Update;
  end POS.Impl;

  -----------------
  -- Subprograms --
  -----------------

  subprogram Update
  features
    this : requires data access POS.Impl;
  properties
    source_language => (C);
    source_name     => "user_update";
    source_text     => ("toy.c");
  end Update;


  subprogram GNC_Job
  properties
    source_language => (C);
    source_name     => "user_gnc_job";
    source_text     => ("toy.c");
  end GNC_Job;

  subprogram TMTC_Job
  properties
    source_language => (C);
    source_name     => "user_tmtc_job";
    source_text     => ("toy.c");
  end TMTC_Job;

  subprogram GNC_Identity
  properties
    source_language => (C);
    source_name     => "user_gnc_identity";
    source_text     => ("toy.c");
  end GNC_Identity;

  subprogram TMTC_Identity
  properties
    source_language => (C);
    source_name     => "user_tmtc_identity";
    source_text     => ("toy.c");
  end TMTC_Identity;

  -------------
  -- Threads --
  -------------

  thread GNC_Thread
  features
    GNC_POS : requires data access POS.Impl;
  end GNC_Thread;

  thread implementation GNC_Thread.Impl
  calls
    Mycalls: {
      Welcome     : subprogram GNC_Identity;
      Update_POS  : subprogram Update;
      GNC_Work    : subprogram GNC_Job;
      Bye         : subprogram GNC_Identity;
    };
  connections
    Cnx1 : data access GNC_POS -> Update_POS.this;
  properties
    Dispatch_Protocol      => Periodic;
    Period                 => 1000 ms;
    Compute_Execution_Time => 0 ms .. 600 ms;
    Deadline               => 1000 ms;
    Priority               => 50;
  end GNC_Thread.Impl;

  thread TMTC_Thread
  features
    TMTC_POS : requires data access POS.Impl;
  end TMTC_Thread;

  thread implementation TMTC_Thread.Impl
  calls
    Mycalls: {
      Welcome   : subprogram TMTC_Identity;
      TMTC_Work : subprogram TMTC_Job;
      Update    : subprogram Update;
      Bye       : subprogram TMTC_Identity;
    };
  connections
    Cnx1 : data access TMTC_POS -> Update.this;
  properties
    Dispatch_Protocol      => Periodic;
    Period                 => 100 ms;
    Compute_Execution_Time => 0 ms .. 50 ms;
    Deadline               => 100 ms;
    Priority               => 20;
  end TMTC_Thread.Impl;

  ---------------
  -- Processes --
  ---------------

  process Toy_Example_Proc
  end Toy_Example_Proc;

  process implementation Toy_Example_Proc.Impl
  subcomponents
    GNC_Th   : thread GNC_Thread.Impl;
    TMTC_Th  : thread TMTC_Thread.Impl;
    POS_Data : data POS.Impl;
  connections
    Cnx1 : data access POS_Data -> GNC_Th.GNC_POS;
    Cnx2 : data access POS_Data -> TMTC_Th.TMTC_POS;
  end Toy_Example_Proc.Impl;

  ---------------
  -- Processor --
  ---------------

  processor the_processor
  properties
    --Deployment::Execution_Platform => native;
    Priority_Range => 0 .. 255;
  end the_processor;

  ------------
  -- System --
  ------------

  system toy_example
  end toy_example;

  system implementation toy_example.native
  subcomponents
    P1           : processor the_processor;
    GNC_TMTC_POS : process Toy_Example_Proc.Impl;
  properties
    Actual_Processor_Binding => reference (P1) applies to GNC_TMTC_POS;
  end toy_example.native;

end Toy_Example_NoWrapper;