-- 系统功能概述：
-- 这是一个数组数据传输测试案例，专门测试结构体数组和多维数组的通信。
-- 系统包含两个线程：生产者线程和消费者线程，通过事件数据端口传输三种不同类型的数组数据。
--
-- 线程功能：
-- 1. Producer线程（周期1000ms，优先级2）：周期性发送三种数组：
--    - 3个坐标的结构体数组（Coordinate数组，每个Coordinate包含经度、纬度、海拔三个32位整数）
--    - 4个坐标的较大结构体数组
--    - 1个整数的简单数组
-- 2. Consumer线程（偶发，优先级1）：接收并处理生产者发送的三种数组数据
--
-- 系统特点：
-- 1. 这是专门的数组传输测试案例，重点关注结构体数组的序列化和通信
-- 2. 包含两种不同大小的结构体数组（3个元素和4个元素），测试数组容量变化的影响
-- 3. 数据结构包含嵌套：Coordinate结构体内部又包含三个Integer_32数据字段
-- 4. 展示了AADL中数组数据类型的定义和维度指定（Dimension => (3)）
-- 5. 同时测试了简单整数数组和复杂结构体数组，形成对比

package Arrays
public
	with Base_Types, Data_Model;

	processor PC_Processor
	end PC_Processor;

	processor implementation PC_Processor.Impl
	end PC_Processor.Impl;

	data Coordinate
	end Coordinate;

	data implementation Coordinate.Impl
		subcomponents
			latitude: data Base_Types::Integer_32;
			longitude: data Base_Types::Integer_32;
			altitude: data Base_Types::Integer_32;
	end Coordinate.Impl;

	data Array_of_Mission
		properties
			Data_Model::Data_Representation => Array;
			Data_Model::Base_Type => (classifier (Coordinate.Impl));
			Data_Model::Dimension => (3);
	end Array_of_Mission;
	
	data Larger_Array_of_Mission
		properties
			Data_Model::Data_Representation => Array;
			Data_Model::Base_Type => (classifier (Coordinate.Impl));
			Data_Model::Dimension => (4);
	end Larger_Array_of_Mission;
	
	data Array_of_Integer
		properties
			Data_Model::Data_Representation => Array;
			Data_Model::Base_Type => (classifier (Base_Types::Integer));
			Data_Model::Dimension => (1);
	end Array_of_Integer;

	system PC_Sys
	end PC_Sys;

	system implementation PC_Sys.Impl
		subcomponents
			proc: process PC_Proc.Impl;
			proc_hw: processor PC_Processor.Impl;
		properties
     		Actual_processor_binding => reference (proc_hw) applies to proc;
	end PC_Sys.Impl;

	process PC_Proc
	end PC_Proc;

	process implementation PC_Proc.Impl
		subcomponents
			producer: thread Producer.Impl;
			consumer: thread Consumer.Impl;
		connections
			c1: port producer.missions_to_consumer -> consumer.missions_from_producer;
			c2: port producer.integers_to_consumer -> consumer.integers_from_producer;
			c3: port producer.larger_missions_to_consumer -> consumer.larger_missions_from_producer;
	end PC_Proc.Impl;

	thread Producer
		features
			--to_filter: out event data port Mission;
			missions_to_consumer: out event data port Array_of_Mission;
			larger_missions_to_consumer: out event data port Larger_Array_of_Mission;
			integers_to_consumer: out event data port Array_of_Integer;
		properties
			Thread_Properties::Dispatch_Protocol => Periodic;
			Timing_Properties::Period => 1000ms;
	end Producer;

	thread implementation Producer.Impl
		properties
			Thread_Properties::Dispatch_Protocol => Periodic;
			Timing_Properties::Period => 1000ms;
			Priority => 2;
	end Producer.Impl;

	thread Consumer
		features
			missions_from_producer: in event data port Array_of_Mission;
			larger_missions_from_producer: in event data port Larger_Array_of_Mission;
			integers_from_producer: in event data port Array_of_Integer;
		properties
			Thread_Properties::Dispatch_Protocol => Sporadic;
	end Consumer;

	thread implementation Consumer.Impl
		properties
			Thread_Properties::Dispatch_Protocol => Sporadic;
			Priority => 1;
	end Consumer.Impl;
end Arrays;