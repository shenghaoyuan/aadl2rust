package aadlbook::platform

public

with SEI;

processor ecu
properties
	SEI::MIPSCapacity => 50.0 MIPS; 
end ecu;

end aadlbook::platform;



package aadlbook::icd

public

with base_types;
with data_model;

--修改了obstacle_position
-- data obstacle_position
-- properties
-- 	Data_Model::Data_Representation => Struct;
-- end obstacle_position;

-- data implementation obstacle_position.i
-- subcomponents
--   present : data base_types::boolean;
--   x       : data Base_Types::Unsigned_8;
-- end obstacle_position.i;
data obstacle_position
properties
	Data_Model::Data_Representation => Boolean;
end obstacle_position;

data speed
properties
	Data_Model::Data_Representation => unsigned_16;
end speed;

data picture 
properties
	data_model::data_representation => array;
	data_model::base_type => classifier (Base_Types::Integer);
	data_model::dimension => (4, 4);
end picture; 

data boolean
properties
	Data_Model::Data_Representation => Boolean;
end boolean;


data pressure
properties
	Data_Model::Data_Representation => Integer_8;
end pressure;

data entertainment_infos
properties
	Data_Model::Data_Representation => Integer_8;
end entertainment_infos;


data speed_cmd
properties
	Data_Model::Data_Representation => Integer_8;
end speed_cmd;

data brake_cmd
properties
	Data_Model::Data_Representation => Integer_8;
end brake_cmd;

data distance
properties
	Data_Model::Data_Representation => unsigned_32;
end distance;

data music
properties
	Data_Model::Data_Representation => Boolean;
end music;


data contacts
properties
	Data_Model::Data_Representation => Integer_8;
end contacts;


end aadlbook::icd;




package aadlbook::software::entertainment

public

with aadlbook::icd; 
with sei;

--------------------------------
--  The entertainment system  --
--------------------------------

process entertainment
features
	music_in  : in data port aadlbook::icd::music;
	contacts  : in data port aadlbook::icd::contacts;
	infos     : out data port aadlbook::icd::entertainment_infos;
	music_out : out data port aadlbook::icd::music;
end entertainment;


process implementation entertainment.i
subcomponents
   enter_thr : thread entertainment_thr.i;
connections
  c0 : port music_in -> enter_thr.music_in;
  c1 : port contacts -> enter_thr.contacts;
  c2 : port enter_thr.infos -> infos;
  c3 : port enter_thr.music_out -> music_out;
end entertainment.i;

thread entertainment_thr
features
	music_in  : in data port aadlbook::icd::music;
	contacts  : in data port aadlbook::icd::contacts;
	infos     : out data port aadlbook::icd::entertainment_infos;
	music_out : out data port aadlbook::icd::music;
properties
	Dispatch_Protocol => Periodic;
	Period            => 5 ms;
	sei::mipsbudget   => 5.0 MIPS;
end entertainment_thr;

thread implementation entertainment_thr.i
annex Behavior_specification {**
	states
	 s0 : initial complete final state;
	 transitions
	 s0 -[on dispatch]-> s0 {
		infos := 16;
		music_out := false
	 };
	**};
end entertainment_thr.i;

end aadlbook::software::entertainment;




package aadlbook::software::image_acquisition

public

with aadlbook::icd; 
with sei;

-------------------------
--  Image Acquisition  --
-------------------------

process image_acquisition
features
	picture           : in data port aadlbook::icd::picture;
	obstacle_detected : out data port aadlbook::icd::obstacle_position;
end image_acquisition;

process implementation image_acquisition.i
subcomponents
	acq_thr : thread image_acquisition_thr.i;
connections
	c0 : port picture -> acq_thr.picture;
	c1 : port acq_thr.obstacle_detected -> obstacle_detected;
end image_acquisition.i;

thread image_acquisition_thr
features
	picture           : in data port aadlbook::icd::picture;
	obstacle_detected : out data port aadlbook::icd::obstacle_position;
properties
	sei::mipsbudget   => 25.0 MIPS;
	Dispatch_Protocol => Periodic;
	Period            => 50 ms;
	compute_execution_time => 10 ms .. 40 ms;
--	reference_processor => classifier (::platform::ecu);
	sei::instructionsperdispatch => 1.24 kipd .. 1.25 mipd;
end image_acquisition_thr;

thread implementation image_acquisition_thr.i
annex Behavior_specification {**
	 states
	 s0 : initial complete final state;
	 transitions
	 s0 -[on dispatch]-> s0 {
		obstacle_detected := false
	 };
	**};
end image_acquisition_thr.i;

end aadlbook::software::image_acquisition;




package aadlbook::software::obstacle_detection

public

with aadlbook::icd; 
with sei;

--------------------------
--  Obstacle Detection  --
--------------------------

process obstacle_detection
features
	camera : in data port aadlbook::icd::obstacle_position;
	radar : in data port aadlbook::icd::obstacle_position;
	obstacle_position : out data port aadlbook::icd::obstacle_position;
end obstacle_detection;

process implementation obstacle_detection.i
subcomponents
	obst_thr : thread obstacle_detection_thr.i;
connections
	c0 : port camera -> obst_thr.camera;
	c1 : port radar -> obst_thr.radar;
	c2 : port obst_thr.obstacle_detected -> obstacle_position;
end obstacle_detection.i;

thread obstacle_detection_thr
features
	camera : in data port aadlbook::icd::obstacle_position;
	radar : in data port aadlbook::icd::obstacle_position;
	obstacle_detected : out data port aadlbook::icd::obstacle_position;
properties
	Dispatch_Protocol => Periodic;
	Period            => 100 ms;
	Compute_Execution_Time => 20 ms .. 50 ms; 
	sei::mipsbudget   => 10.0 MIPS; 
	sei::instructionsperdispatch => 0.9 kipd .. 0.98 mipd; 
end obstacle_detection_thr;

thread implementation obstacle_detection_thr.i
annex Behavior_specification {**
	variables
	 obstacle_detected_temp : Base_Types::Boolean := false;
	 states
	 s0 : initial complete final state;
	 s1 : state;
	 transitions
	 s0 -[camera]-> s0 {
		obstacle_detected := true
	 };
	 s0 -[not camera]-> s1{
		obstacle_detected_temp := false
	 };
	 s0 -[radar]-> s0 {
		obstacle_detected := true
	 };
	 s0 -[not radar]-> s0 {
		obstacle_detected_temp := false
	 };
	 s1 -[on dispatch]-> s0 {
		obstacle_detected := obstacle_detected_temp
	 }; 
	**};
end obstacle_detection_thr.i;

end aadlbook::software::obstacle_detection;




package aadlbook::software::panel_control
public

with aadlbook::icd;

process panel_control
features
	increase_speed : in event port aadlbook::icd::speed;
	decrease_speed : in event port aadlbook::icd::speed;
	current_speed : in data port aadlbook::icd::speed;
	desired_speed : out data port aadlbook::icd::speed;
	tire_pressure_in : in data port aadlbook::icd::pressure;
	tire_pressure_out : out data port aadlbook::icd::pressure; 
end panel_control;

process implementation panel_control.i
subcomponents
	panel_thr : thread panel_control_thr.i;
connections
	c0 : port increase_speed -> panel_thr.increase_speed;
	c1 : port decrease_speed -> panel_thr.decrease_speed;
	c2 : port current_speed -> panel_thr.current_speed;
	c3 : port tire_pressure_in -> panel_thr.tire_pressure_in;
	c4 : port panel_thr.tire_pressure_out -> tire_pressure_out;
	c5 : port panel_thr.desired_speed -> desired_speed; 
end panel_control.i;

thread panel_control_thr
features
	increase_speed : in event port aadlbook::icd::speed;
	decrease_speed : in event port aadlbook::icd::speed;
	current_speed : in data port aadlbook::icd::speed;
	desired_speed : out data port aadlbook::icd::speed;
	tire_pressure_in : in data port aadlbook::icd::pressure;
	tire_pressure_out : out data port aadlbook::icd::pressure; 
end panel_control_thr;

--只是简单的逻辑，为了示意
thread implementation panel_control_thr.i
annex Behavior_specification {**
	states
	 s0 : initial complete final state;
	 s1 : state;
	 s2 : state;
	 transitions
	 s0 -[0 < current_speed]-> s1 {
		desired_speed := current_speed + 1
	 };
	 s1 -[0 < tire_pressure_in]-> s2 {
		tire_pressure_out := tire_pressure_in
	 };
	 s2 -[]-> s0;
	**};
end panel_control_thr.i;

end aadlbook::software::panel_control;




package aadlbook::software::speed_controller

public

with aadlbook::icd; 
with sei;

------------------------
--  Speed Controller  --
------------------------

process speed_controller
features
	obstacle_position : in data port aadlbook::icd::obstacle_position;
	current_speed     : in data port aadlbook::icd::speed;
	desired_speed     : in data port aadlbook::icd::speed;
	brake_cmd         : out data port aadlbook::icd::brake_cmd;
	speed_cmd         : out data port aadlbook::icd::speed_cmd;
	warning           : out data port aadlbook::icd::boolean;
	
end speed_controller;

process implementation speed_controller.i
subcomponents
	accel_thr : thread speed_controller_accel_thr.i;
	brake_thr : thread speed_controller_brake_thr.i;
	warning_thr : thread speed_controller_warning_thr.i;
connections
	c00 : port obstacle_position    -> accel_thr.obstacle_position;
	c01 : port current_speed        -> accel_thr.current_speed;
	c02 : port desired_speed        -> accel_thr.desired_speed;
	c03 : port accel_thr.speed_cmd  -> speed_cmd;

	c10 : port obstacle_position    -> brake_thr.obstacle_position;
	c11 : port current_speed        -> brake_thr.current_speed;
	c12 : port desired_speed        -> brake_thr.desired_speed;
	c13 : port brake_thr.brake_cmd  -> brake_cmd;

	c20 : port obstacle_position    -> warning_thr.obstacle_position;
	c21 : port current_speed        -> warning_thr.current_speed;
	c22 : port desired_speed        -> warning_thr.desired_speed;
	c23 : port warning_thr.warning  -> warning;
end speed_controller.i;

thread speed_controller_warning_thr
features
	obstacle_position : in data port aadlbook::icd::obstacle_position;
	current_speed     : in data port aadlbook::icd::speed;
	desired_speed     : in data port aadlbook::icd::speed;
	warning         : out data port aadlbook::icd::boolean;
properties
	Dispatch_Protocol => Periodic;
	Period            => 5 ms;
	sei::mipsbudget   => 5.0 MIPS;
end speed_controller_warning_thr;

thread speed_controller_brake_thr
features
	obstacle_position : in data port aadlbook::icd::obstacle_position;
	current_speed     : in data port aadlbook::icd::speed;
	desired_speed     : in data port aadlbook::icd::speed;
	brake_cmd         : out data port aadlbook::icd::brake_cmd;
properties
	Dispatch_Protocol => Periodic;
	Period            => 5 ms;
	sei::mipsbudget   => 5.0 MIPS;
end speed_controller_brake_thr;

thread speed_controller_accel_thr
features
	obstacle_position : in data port aadlbook::icd::obstacle_position;
	current_speed     : in data port aadlbook::icd::speed;
	desired_speed     : in data port aadlbook::icd::speed;
	speed_cmd         : out data port aadlbook::icd::speed_cmd;
properties
	Dispatch_Protocol => Periodic;
	Period            => 5 ms;
	sei::mipsbudget   => 5.0 MIPS;
end speed_controller_accel_thr;

thread implementation speed_controller_accel_thr.i
annex Behavior_specification {**
	states
	 s0 : initial complete final state;
	 transitions
	 s0 -[60 < current_speed]-> s0 {
		speed_cmd := 0
	 };
	**};
end speed_controller_accel_thr.i;

thread implementation speed_controller_brake_thr.i
annex Behavior_specification {**
	states
	 s0 : initial complete final state;
	 transitions
	 s0 -[80 < current_speed]-> s0 {
		brake_cmd := 10
	 };
	**};
end speed_controller_brake_thr.i;

thread implementation speed_controller_warning_thr.i
annex Behavior_specification {**
	states
	 s0 : initial complete final state;
	 transitions
	 s0 -[obstacle_position]-> s0 {
		warning := true
	 };
	**};
end speed_controller_warning_thr.i;

end aadlbook::software::speed_controller;



package aadlbook::software::speed_voter

public

with aadlbook::icd; 
with sei;



-------------------
--  Speed Voter  --
-------------------

process speed_voter
features
	wheel_sensor : in data port aadlbook::icd::speed;
	laser_sensor : in data port aadlbook::icd::speed;
	speed        : out data port aadlbook::icd::speed;
end speed_voter;

process implementation speed_voter.i
subcomponents
	speed_thr : thread speed_voter_thr;
connections
	c0 : port wheel_sensor -> speed_thr.wheel_sensor;
	c1 : port laser_sensor -> speed_thr.laser_sensor;
	c2 : port speed_thr.speed -> speed;
end speed_voter.i;

thread speed_voter_thr
features
	wheel_sensor : in data port aadlbook::icd::speed;
	laser_sensor : in data port aadlbook::icd::speed;
	speed        : out data port aadlbook::icd::speed;
properties
	Dispatch_Protocol => Periodic;
	Period            => 8 ms;
	sei::mipsbudget   => 8.0 MIPS;
end speed_voter_thr;

thread implementation speed_voter_thr.i
annex Behavior_specification {**
	variables
	 speed_value : Base_Types::Unsigned_16 := 0;
	 states
	 s0 : initial complete final state;
	 s1 : state;
	 transitions
	 s0 -[0 < wheel_sensor]-> s1 {
		speed_value := wheel_sensor
	 };
	 s1 -[0 < laser_sensor]-> s0 {
	 speed_value := laser_sensor + speed_value;
	 speed := speed_value/2
	 };
	**};
end speed_voter_thr.i;


end aadlbook::software::speed_voter;

package aadlbook::devices


public

with aadlbook::icd;
with aadlbook::platform;
 
-----------------------------------
--  Camera and Object Detection  --
-----------------------------------
   
device camera
features
	picture : out data port aadlbook::icd::picture;
properties 
	Period => 2000 ms;
	compute_execution_time => 20 ms .. 50 ms;
end camera;

device radar
features
	distance_estimate : out data port aadlbook::icd::obstacle_position;
properties 
	Period => 1000 ms;
	compute_execution_time => 6 ms .. 8 ms;
end radar;
 
---------------------
--  Speed Sensors  --
---------------------

device speed_wheel_sensor
features
	speed : out data port aadlbook::icd::speed;
properties
	Period => 1000 ms;
	compute_execution_time => 6 ms .. 9 ms;
end speed_wheel_sensor;

device speed_laser_sensor
features
	speed : out data port aadlbook::icd::speed;
properties
	Period => 1000 ms;
	compute_execution_time => 6 ms .. 9 ms;
end speed_laser_sensor;


-------------
--  Brake  --
-------------

device brake 
features
	cmd : in data port aadlbook::icd::brake_cmd;
properties
	Period => 100 ms; 
end brake;

device acceleration
features
	cmd : in data port aadlbook::icd::speed_cmd;
properties
	Period => 100 ms;
end acceleration;

device panel
features
	increase_speed : out data port aadlbook::icd::speed;
	decrease_speed : out data port aadlbook::icd::speed;
end panel;

device screen
features
	tire_pressure : in data port aadlbook::icd::pressure;
	desired_speed : in data port aadlbook::icd::speed;
	actual_speed : in data port aadlbook::icd::speed;
	warning : in data port aadlbook::icd::boolean;
	entertainment_infos : in data port aadlbook::icd::entertainment_infos;
end screen;

device tpms
features
	pressure : out data port aadlbook::icd::pressure;
end tpms;

device bluetooth_controller
features
	music     : out data port aadlbook::icd::music;
	contacts  : out data port aadlbook::icd::contacts;
end bluetooth_controller;

device speaker
features
	music  : in data port aadlbook::icd::music;
end speaker;

end aadlbook::devices;


package aadlbook::integration

public

with aadlbook::devices;
with aadlbook::software::image_acquisition;
with aadlbook::software::obstacle_detection;
with aadlbook::software::panel_control;
with aadlbook::software::speed_controller;
with aadlbook::software::speed_voter;
with aadlbook::software::entertainment;
with aadlbook::platform;

system integration
end integration;

--
--  This is the functional system. This system is designed to be independent
--  of the underlying deployment platform. It contains all components
--  without associating connections or processes to processors.
--
--  The association to an execution platform is done later, in the
--  system implementation integration.variation1 and integration.variation2
--

system implementation integration.functional
subcomponents
-- input devices
	obstacle_camera_dev    : device aadlbook::devices::camera;
	obstacle_radar_dev     : device aadlbook::devices::radar;
	wheel_sensor_dev       : device aadlbook::devices::speed_wheel_sensor;
	laser_sensor_dev       : device aadlbook::devices::speed_laser_sensor;
	panel_dev              : device aadlbook::devices::panel;
	tire_pressure_dev      : device aadlbook::devices::tpms;
	bluetooth_ctrl_dev     : device aadlbook::devices::bluetooth_controller;

-- software and processing elements.
    image_acquisition  : process aadlbook::software::image_acquisition::image_acquisition.i;
    obstacle_detection : process aadlbook::software::obstacle_detection::obstacle_detection.i;
    panel_controller   : process aadlbook::software::panel_control::panel_control.i;
    speed_voter        : process aadlbook::software::speed_voter::speed_voter.i;
    speed_ctrl         : process aadlbook::software::speed_controller::speed_controller.i;
    entertainment      : process aadlbook::software::entertainment::entertainment.i;

-- output devices
	brake_dev         : device aadlbook::devices::brake;
	acceleration_dev  : device aadlbook::devices::acceleration;
	screen_dev        : device aadlbook::devices::screen;
	speaker_dev       : device aadlbook::devices::speaker;
connections
    c00 : port obstacle_camera_dev.picture               -> image_acquisition.picture;
    c01 : port image_acquisition.obstacle_detected   -> obstacle_detection.camera; 
    c02 : port obstacle_radar_dev.distance_estimate      -> obstacle_detection.radar;
    c03 : port obstacle_detection.obstacle_position  -> speed_ctrl.obstacle_position;
    c04 : port wheel_sensor_dev.speed                    -> speed_voter.wheel_sensor;
    c05 : port laser_sensor_dev.speed                    -> speed_voter.laser_sensor;
    c06 : port speed_voter.speed                     -> speed_ctrl.current_speed;
    c07 : port speed_voter.speed                     -> screen_dev.actual_speed;
    c08 : port speed_ctrl.speed_cmd                  -> acceleration_dev.cmd;
    c09 : port speed_ctrl.brake_cmd                  -> brake_dev.cmd;
    c10 : port speed_ctrl.warning                    -> screen_dev.warning;
    c11 : port panel_dev.increase_speed                  -> panel_controller.increase_speed;
    c12 : port panel_dev.decrease_speed                  -> panel_controller.decrease_speed;
    c13 : port panel_controller.desired_speed        -> speed_ctrl.desired_speed;
    c14 : port speed_voter.speed                     -> panel_controller.current_speed;
    c15 : port panel_controller.desired_speed        -> screen_dev.desired_speed;
	c16 : port bluetooth_ctrl_dev.contacts               -> entertainment.contacts;
	c17 : port bluetooth_ctrl_dev.music                  -> entertainment.music_in;
	c18 : port entertainment.music_out               -> speaker_dev.music;
	c19 : port entertainment.infos                   -> screen_dev.entertainment_infos;
	c20 : port panel_controller.tire_pressure_out    -> tire_pressure_dev.pressure;
	c21 : port panel_controller.tire_pressure_in                -> tire_pressure_dev.pressure;
end integration.functional;
end aadlbook::integration;