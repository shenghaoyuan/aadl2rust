-- 系统功能概述：
-- 这是一个优先级测试案例，专门用于进行可调度性分析和周期性任务优先级行为研究。
-- 系统包含三个独立的周期性任务，它们之间没有交互，运行在同一处理器上，用于分析优先级调度策略。
--
-- 线程功能：
-- 1. Task1（周期600ms，优先级30）：调用task_1函数，最高优先级任务
-- 2. Task2（周期800ms，优先级20）：调用task_2函数，具有200ms启动偏移（Dispatch_offset）
-- 3. Task3（周期1200ms，优先级10）：调用task_3函数，最低优先级任务
--
-- 系统特点：
-- 1. 专门用于配合Cheddar等调度分析工具
-- 2. 三个任务具有不同优先级（30>20>10）和不同周期（600ms<800ms<1200ms）
-- 3. Task2设置了启动偏移（200ms），用于避免任务同时启动导致的瞬时高负载
-- 4. 任务之间没有通信连接，专注于调度行为分析
-- 5. 优先级范围0-255，使用POSIX最高优先级优先调度协议

--  This AADL model illustrates how to conduct schedulability analysis
--  using Cheddar, and then code generation of periodic tasks.
--
--  Three periodic tasks run in parrallel, without interaction, in order
--  to analyse Periodic Priority behavior.
--
--  Study Case Cottet, F., & Grolleau, E. (2005). Systèmes temps réel de contrôle-commande:
--  conception et implémentation. Dunod. Page 501. Using RTEMS.

package PTEST
public
  -- with Deployment;

  -----------------
  -- Subprograms --
  -----------------

  subprogram Ptest_Spg_1
  properties
    source_language => (C);
    source_name     => "task_1";
    source_text     => ("ptest.c");
  end Ptest_Spg_1;

  subprogram Ptest_Spg_2
  properties
    source_language => (C);
    source_name     => "task_2";
    source_text     => ("ptest.c");
  end Ptest_Spg_2;

  subprogram Ptest_Spg_3
  properties
    source_language => (C);
    source_name     => "task_3";
    source_text     => ("ptest.c");
  end Ptest_Spg_3;

  -------------
  -- Threads --
  -------------

  thread Task1
  end Task1;

  thread implementation Task1.impl_1
  calls
    Mycalls: {
    P_Spg : subprogram Ptest_Spg_1;
    };
  properties
    Dispatch_Protocol                  => Periodic;
    Period                             => 600ms;
    Priority => 30;
  end Task1.impl_1;

  thread Task2
  end Task2;

  thread implementation Task2.impl_2
  calls
    Mycalls: {
    P_Spg : subprogram Ptest_Spg_2;
    };
  properties
    Dispatch_Protocol                  => Periodic;
    Period                             => 800ms;
    Priority => 20;
    Dispatch_offset                    => 200ms;
  end Task2.impl_2;

  thread Task3
  end Task3;

  thread implementation Task3.impl_3
  calls
    Mycalls: {
    P_Spg : subprogram Ptest_Spg_3;
    };
  properties
    Dispatch_Protocol                  => Periodic;
    Period                             => 1200ms;
    Priority => 10;
  end Task3.impl_3;

  ---------------
  -- Processor --
  ---------------

  processor cpurm
  -- properties
  --   Deployment::Execution_Platform => native;
  end cpurm;

  processor implementation cpurm.impl
  properties
    Scheduling_Protocol => (Posix_1003_Highest_Priority_First_Protocol);
    Priority_Range => 0 .. 255;
  end cpurm.impl;

  ---------------
  -- Processes --
  ---------------

  process node_a
  end node_a;

  process implementation node_a.impl
  subcomponents
    Task1 : thread Task1.impl_1;
    Task2 : thread Task2.impl_2;
    Task3 : thread Task3.impl_3;
  end node_a.impl;

  ------------
  -- System --
  ------------

  system ptest
  end ptest;

  system implementation ptest.impl
  subcomponents
    ptest : process node_a.impl;
    cpu_rm : processor cpurm.impl;
  properties
    Actual_Processor_Binding => reference (cpu_rm) applies to ptest;
  end ptest.impl;

  -- system implementation ptest.rtems_impl extends ptest.impl
  -- properties
  --   Deployment::Execution_platform => LEON_RTEMS_POSIX applies to cpu_rm;
  -- end ptest.rtems_impl;

end PTEST;
